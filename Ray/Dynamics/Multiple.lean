import Mathlib.RingTheory.RootsOfUnity.Complex
import Ray.AnalyticManifold.Inverse
import Ray.AnalyticManifold.LocalInj
import Ray.Dynamics.BottcherNear

/-!
## Non-injectivity near multiple roots

Let `f : S ‚Üí T` be an analytic function between 1D complex manifolds.  We show that if
`f` has zero derivative at a point, it is not locally injective near that point.  Indeed,
we show that there is a nontrivial local nonlinear rotation `g : S ‚Üí S` around the point
that locally commutes with `f`: `f (g z) = f z` and `g z ‚â† z` except at the point.

This is a bit of a sledgehammer, as (1) the rotation `g` is defined using B√∂ttcher coordinates
and so far we use only (2) the fact that injectivity implies nonzero derivative.  There are
surely simpler proofs of (2), but it's nice to have the rotation fact, and we already have
B√∂ttcher coordinates.

The proof proceeds in w.l.o.g. stages, reducing first from manifolds to `‚ÑÇ ‚Üí ‚ÑÇ`, then moving
the point to `0` and standardizing the leading coefficient to be 1.
-/

open Complex (exp log abs cpow)
open Filter (Tendsto atTop)
open Function (curry uncurry)
open Metric (ball closedBall isOpen_ball ball_mem_nhds mem_ball_self nonempty_ball)
open Nat (iterate)
open OneDimension
open Set
open scoped NNReal Topology Real
noncomputable section

variable {S : Type} [TopologicalSpace S] [ChartedSpace ‚ÑÇ S] [AnalyticManifold I S]
variable {T : Type} [TopologicalSpace T] [ChartedSpace ‚ÑÇ T] [AnalyticManifold I T]

/-- There are nontrivial `d`th roots of unity if `2 ‚â§ d` -/
theorem exist_root_of_unity {d : ‚Ñï} (d2 : 2 ‚â§ d) : ‚àÉ a : ‚ÑÇ, a ‚â† 1 ‚àß a ^ d = 1 := by
  set n : ‚Ñï+ := ‚ü®d, lt_of_lt_of_le (by norm_num) d2‚ü©
  have two : Nontrivial (rootsOfUnity n ‚ÑÇ) := by
    rw [‚Üê Fintype.one_lt_card_iff_nontrivial, Complex.card_rootsOfUnity]
    simp only [PNat.mk_coe, n]; exact lt_of_lt_of_le (by norm_num) d2
  rcases two with ‚ü®‚ü®a, am‚ü©, ‚ü®b, bm‚ü©, ab‚ü©
  simp only [Ne, Subtype.mk_eq_mk, mem_rootsOfUnity, PNat.mk_coe] at am bm ab
  by_cases a1 : a = 1
  ¬∑ use b; rw [a1] at ab; constructor
    ¬∑ simp only [ne_eq, Units.val_eq_one, Ne.symm ab, not_false_eq_true]
    ¬∑ simp only [PNat.mk_coe, n] at bm; rw [‚Üê Units.val_pow_eq_pow_val, bm, Units.val_one]
  ¬∑ use a; constructor
    ¬∑ simp only [ne_eq, Units.val_eq_one, a1, not_false_eq_true]
    ¬∑ simp only [PNat.mk_coe, n] at am; rw [‚Üê Units.val_pow_eq_pow_val, am, Units.val_one]

/-- Case `c = 0, f 0 = 0`, when `f` has a monic, superattracting fixpoint at 0.  Every
    nearby point is achieved at least twice.  We operationalize this statement via a
    nontrivial function `g : ‚ÑÇ ‚Üí ‚ÑÇ` s.t. `f (g z) = f z`. -/
theorem SuperAt.not_local_inj {f : ‚ÑÇ ‚Üí ‚ÑÇ} {d : ‚Ñï} (s : SuperAt f d) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g 0 ‚àß g 0 = 0 ‚àß ‚àÄ·∂† z in ùìù[{0}·∂ú] 0, g z ‚â† z ‚àß f (g z) = f z := by
  rcases s.superNear with ‚ü®t, s‚ü©
  have ba : AnalyticAt ‚ÑÇ (bottcherNear f d) 0 := bottcherNear_analytic_z s _ s.t0
  have nc : mfderiv I I (bottcherNear f d) 0 ‚â† 0 := by
    rw [mfderiv_eq_fderiv, ‚Üê deriv_fderiv, (bottcherNear_monic s).deriv]
    exact ContinuousLinearMap.smulRight_ne_zero ContinuousLinearMap.one_ne_zero (by norm_num)
  rcases complex_inverse_fun' (ba.mAnalyticAt I I) nc with ‚ü®i, ia, ib, bi‚ü©
  rw [bottcherNear_zero] at bi ia
  have i0 : i 0 = 0 := by nth_rw 1 [‚Üê bottcherNear_zero]; rw [ib.self_of_nhds]
  have inj : ‚àÄ·∂† p : ‚ÑÇ √ó ‚ÑÇ in ùìù (0, 0), i p.1 = i p.2 ‚Üí p.1 = p.2 := by
    refine ia.local_inj ?_
    have d0 : mfderiv I I (fun z : ‚ÑÇ ‚Ü¶ z) 0 ‚â† 0 := id_mderiv_ne_zero
    rw [(Filter.EventuallyEq.symm ib).mfderiv_eq] at d0
    rw [‚ÜêFunction.comp_def, mfderiv_comp 0 _ ba.differentiableAt.mdifferentiableAt] at d0
    simp only [Ne, mderiv_comp_eq_zero_iff, nc, or_false] at d0
    rw [bottcherNear_zero] at d0; exact d0
    rw [bottcherNear_zero]; exact ia.mdifferentiableAt
  rcases exist_root_of_unity s.d2 with ‚ü®a, a1, ad‚ü©
  refine ‚ü®fun z ‚Ü¶ i (a * bottcherNear f d z), ?_, ?_, ?_‚ü©
  ¬∑ apply MAnalyticAt.analyticAt I I
    refine ia.comp_of_eq (mAnalyticAt_const.mul (ba.mAnalyticAt I I)) ?_
    simp only [bottcherNear_zero, s.f0, MulZeroClass.mul_zero]
  ¬∑ simp only [bottcherNear_zero, MulZeroClass.mul_zero, i0]
  ¬∑ simp only [eventually_nhdsWithin_iff, mem_compl_singleton_iff]
    have t0 : ContinuousAt (fun z ‚Ü¶ a * bottcherNear f d z) 0 :=
      continuousAt_const.mul ba.continuousAt
    have t1 : ContinuousAt (fun z ‚Ü¶ f (i (a * bottcherNear f d z))) 0 := by
      refine s.fa0.continuousAt.comp_of_eq (ia.continuousAt.comp_of_eq t0 ?_) ?_
      repeat' simp only [bottcherNear_zero, MulZeroClass.mul_zero, i0]
    have t2 : ContinuousAt f 0 := s.fa0.continuousAt
    have m0 : ‚àÄ·∂† z in ùìù 0, i (a * bottcherNear f d z) ‚àà t := by
      refine (ia.continuousAt.comp_of_eq t0 ?_).eventually_mem (s.o.mem_nhds ?_)
      repeat' simp only [bottcherNear_zero, MulZeroClass.mul_zero, i0, s.t0, Function.comp_def]
    have m1 : ‚àÄ·∂† z in ùìù 0, z ‚àà t := s.o.eventually_mem s.t0
    simp only [ContinuousAt, bottcherNear_zero, MulZeroClass.mul_zero, i0, s.f0] at t0 t1 t2
    have tp := t0.prod_mk ba.continuousAt
    simp only [‚Üê nhds_prod_eq, ContinuousAt, bottcherNear_zero] at tp
    apply (tp.eventually inj).mp
    refine ib.mp (bi.mp ((t1.eventually ib).mp
      ((t0.eventually bi).mp ((t2.eventually ib).mp (m0.mp (m1.mp ?_))))))
    refine .of_forall fun z m1 m0 t2 t0 t1 _ ib tp z0 ‚Ü¶ ‚ü®?_, ?_‚ü©
    ¬∑ contrapose tp; simp only [ne_eq, Decidable.not_not, Classical.not_imp] at tp ‚ä¢
      rw [ib]; use tp
      contrapose a1; simp only [not_not] at a1 ‚ä¢
      have b0 := bottcherNear_ne_zero s m1 z0
      calc a
        _ = a * bottcherNear f d z / bottcherNear f d z := by field_simp [b0]
        _ = bottcherNear f d z / bottcherNear f d z := by rw [a1]
        _ = 1 := div_self b0
    ¬∑ rw [‚Üê t1, bottcherNear_eqn s m0, t0, mul_pow, ad, one_mul, ‚Üê bottcherNear_eqn s m1, t2]

/-- Case `c = 0, f 0 = 0, f' 0 = 0`.  Every nearby point is achieved at least twice.  We
    operationalize this statement via a nontrivial function `g : ‚ÑÇ ‚Üí ‚ÑÇ` s.t. `f (g z) = f z`. -/
theorem not_local_inj_of_deriv_zero' {f : ‚ÑÇ ‚Üí ‚ÑÇ} (fa : AnalyticAt ‚ÑÇ f 0) (df : HasDerivAt f 0 0)
    (f0 : f 0 = 0) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g 0 ‚àß g 0 = 0 ‚àß ‚àÄ·∂† z in ùìù[{0}·∂ú] 0, g z ‚â† z ‚àß f (g z) = f z := by
  by_cases o0 : orderAt f 0 = 0
  ¬∑ simp only [orderAt_eq_zero_iff fa, f0, Ne, eq_self_iff_true, not_true, or_false] at o0
    use fun z ‚Ü¶ -z, analyticAt_id.neg, neg_zero; rw [eventually_nhdsWithin_iff]
    have e0 : ‚àÄ·∂† z in ùìù 0, f (-z) = 0 := by
      nth_rw 1 [‚Üê neg_zero] at o0; exact continuousAt_neg.eventually o0
    refine o0.mp (e0.mp (.of_forall fun z f0' f0 z0 ‚Ü¶ ?_))
    simp only [mem_compl_singleton_iff] at z0; rw [Pi.zero_apply] at f0
    rwa [f0, f0', eq_self_iff_true, and_true, neg_ne_self ‚ÑÇ]
  have o1 : orderAt f 0 ‚â† 1 := by
    have d := df.deriv; contrapose d; simp only [not_not] at d
    exact deriv_ne_zero_of_orderAt_eq_one d
  have d2 : 2 ‚â§ orderAt f 0 := by rw [Nat.two_le_iff]; use o0, o1
  clear o1 df f0
  set a := leadingCoeff f 0
  have a0 : a ‚â† 0 := leadingCoeff_ne_zero fa o0
  set g := fun z ‚Ü¶ a‚Åª¬π ‚Ä¢ f z
  have s : SuperAt g (orderAt f 0) :=
    { d2
      fa0 := analyticAt_const.mul fa
      fd := by rw [orderAt_const_smul (inv_ne_zero a0)]
      fc := by rw [leadingCoeff_const_smul]; simp only [smul_eq_mul, inv_mul_cancel‚ÇÄ a0] }
  rcases s.not_local_inj with ‚ü®h, ha, h0, e‚ü©
  use h, ha, h0; refine e.mp (.of_forall ?_)
  intro z ‚ü®h0, hz‚ü©; use h0
  exact (IsUnit.smul_left_cancel (Ne.isUnit (inv_ne_zero a0))).mp hz

/-- If `f' z = 0`, then every value near `f z` is achieved at least twice (`‚ÑÇ ‚Üí ‚ÑÇ` version).
    We operationalize this statement via a nontrivial function `g : ‚ÑÇ ‚Üí ‚ÑÇ` s.t. `f (g w) = f w`
    near `z`. -/
theorem not_local_inj_of_deriv_zero {f : ‚ÑÇ ‚Üí ‚ÑÇ} {c : ‚ÑÇ} (fa : AnalyticAt ‚ÑÇ f c)
    (df : HasDerivAt f 0 c) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g c ‚àß g c = c ‚àß ‚àÄ·∂† z in ùìù[{c}·∂ú] c, g z ‚â† z ‚àß f (g z) = f z := by
  set f' := fun z ‚Ü¶ f (z + c) - f c
  have fa' : AnalyticAt ‚ÑÇ f' 0 :=
    AnalyticAt.sub
      (AnalyticAt.comp (by simp only [zero_add, fa]) (analyticAt_id.add analyticAt_const))
      analyticAt_const
  have df' : HasDerivAt f' (0 * 1) 0 := by
    refine HasDerivAt.sub_const ?_ _
    have e : (fun z ‚Ü¶ f (z + c)) = f ‚àò fun z ‚Ü¶ z + c := rfl
    rw [e]; apply HasDerivAt.comp; simp only [zero_add, df]
    exact HasDerivAt.add_const (hasDerivAt_id _) _
  simp only [MulZeroClass.zero_mul] at df'
  have f0' : (fun z ‚Ü¶ f (z + c) - f c) 0 = 0 := by simp only [zero_add, sub_self]
  rcases not_local_inj_of_deriv_zero' fa' df' f0' with ‚ü®g, ga, e, h‚ü©; clear fa df fa' df'
  refine ‚ü®fun z ‚Ü¶ g (z - c) + c, ?_, ?_, ?_‚ü©
  ¬∑ exact AnalyticAt.add (AnalyticAt.comp (by simp only [sub_self, ga])
      (analyticAt_id.sub analyticAt_const)) analyticAt_const
  ¬∑ simp only [sub_self, e, zero_add]
  ¬∑ simp only [eventually_nhdsWithin_iff] at h ‚ä¢
    have sc : Tendsto (fun z ‚Ü¶ z - c) (ùìù c) (ùìù 0) := by
      rw [‚Üê sub_self c]; exact continuousAt_id.sub continuousAt_const
    refine (sc.eventually h).mp (.of_forall ?_)
    simp only [mem_compl_singleton_iff, sub_ne_zero]
    intro z h zc; rcases h zc with ‚ü®gz, ff‚ü©; constructor
    contrapose gz; simp only [not_not] at gz ‚ä¢; nth_rw 2 [‚Üê gz]; ring
    simp only [sub_left_inj, sub_add_cancel, f'] at ff; exact ff

/-- If `f' z = 0`, then every value near `f z` is achieved at least twice (manifold version).
    We operationalize this statement via a nontrivial function `g : S ‚Üí T` s.t. `f (g w) = f w`
    near `z`. -/
theorem not_local_inj_of_mfderiv_zero {f : S ‚Üí T} {c : S} (fa : MAnalyticAt I I f c)
    (df : mfderiv I I f c = 0) :
    ‚àÉ g : S ‚Üí S, MAnalyticAt I I g c ‚àß g c = c ‚àß ‚àÄ·∂† z in ùìù[{c}·∂ú] c, g z ‚â† z ‚àß f (g z) = f z := by
  generalize hg : (fun z ‚Ü¶ extChartAt I (f c) (f ((extChartAt I c).symm z))) = g
  have dg : mfderiv I I g (extChartAt I c c) = 0 := by
    have fd : MDifferentiableAt I I f ((extChartAt I c).symm (extChartAt I c c)) := by
      rw [PartialEquiv.left_inv]; exact fa.mdifferentiableAt; apply mem_extChartAt_source
    rw [‚Üê hg, ‚ÜêFunction.comp_def, mfderiv_comp _ (MAnalyticAt.extChartAt _).mdifferentiableAt _,
      ‚ÜêFunction.comp_def, mfderiv_comp _ fd (MAnalyticAt.extChartAt_symm _).mdifferentiableAt,
      PartialEquiv.left_inv, df, ContinuousLinearMap.zero_comp, ContinuousLinearMap.comp_zero]
    apply mem_extChartAt_source; apply mem_extChartAt_target; rw [PartialEquiv.left_inv]
    apply mem_extChartAt_source; apply mem_extChartAt_source
    exact MDifferentiableAt.comp _ fd
      (MAnalyticAt.extChartAt_symm (mem_extChartAt_target _)).mdifferentiableAt
  simp only [mAnalyticAt_iff_of_boundaryless, Function.comp_def, hg] at fa
  have dg' := fa.2.differentiableAt.mdifferentiableAt.hasMFDerivAt
  rw [dg, hasMFDerivAt_iff_hasFDerivAt] at dg'
  replace dg := dg'.hasDerivAt; clear dg'
  rcases not_local_inj_of_deriv_zero fa.2 dg with ‚ü®h, ha, h0, e‚ü©
  refine ‚ü®fun z ‚Ü¶ (extChartAt I c).symm (h (extChartAt I c z)), ?_, ?_, ?_‚ü©
  ¬∑ apply (MAnalyticAt.extChartAt_symm (mem_extChartAt_target c)).comp_of_eq
    apply (ha.mAnalyticAt I I).comp_of_eq
      (MAnalyticAt.extChartAt (mem_extChartAt_source c)) rfl
    exact h0
  ¬∑ simp only [h0, PartialEquiv.left_inv _ (mem_extChartAt_source c)]
  ¬∑ rw [eventually_nhdsWithin_iff] at e ‚ä¢
    apply ((continuousAt_extChartAt c).eventually e).mp
    apply ((isOpen_extChartAt_source c).eventually_mem (mem_extChartAt_source (I := I) c)).mp
    have m1 : ‚àÄ·∂† z in ùìù c, h (extChartAt I c z) ‚àà (extChartAt I c).target := by
      refine ContinuousAt.eventually_mem ?_ (extChartAt_target_mem_nhds' ?_)
      ¬∑ exact ha.continuousAt.comp_of_eq (continuousAt_extChartAt c) rfl
      ¬∑ rw [h0]; exact mem_extChartAt_target c
    have m2 : ‚àÄ·∂† z in ùìù c, f z ‚àà (extChartAt I (f c)).source :=
      fa.1.eventually_mem (extChartAt_source_mem_nhds _)
    have m3 : ‚àÄ·∂† z in ùìù c,
        f ((extChartAt I c).symm (h (extChartAt I c z))) ‚àà (extChartAt I (f c)).source := by
      refine ContinuousAt.eventually_mem ?_ (extChartAt_source_mem_nhds' ?_)
      ¬∑ apply fa.1.comp_of_eq; apply (continuousAt_extChartAt_symm _).comp_of_eq
        apply ha.continuousAt.comp_of_eq; exact continuousAt_extChartAt _
        rfl; exact h0; rw [h0, PartialEquiv.left_inv _ (mem_extChartAt_source _)]
      ¬∑ rw [h0, PartialEquiv.left_inv _ (mem_extChartAt_source _)]
        apply mem_extChartAt_source
    refine m1.mp (m2.mp (m3.mp (.of_forall ?_)))
    simp only [mem_compl_singleton_iff]
    intro z m3 m2 m1 m0 even zc
    rcases even ((PartialEquiv.injOn _).ne m0 (mem_extChartAt_source c) zc) with ‚ü®hz, gh‚ü©
    constructor
    ¬∑ nth_rw 2 [‚Üê PartialEquiv.left_inv _ m0]
      rw [(PartialEquiv.injOn _).ne_iff]; exact hz
      rw [PartialEquiv.symm_source]; exact m1
      rw [PartialEquiv.symm_source]; exact PartialEquiv.map_source _ m0
    ¬∑ simp only [‚Üê hg] at gh
      rw [PartialEquiv.left_inv _ m0] at gh
      rw [(PartialEquiv.injOn _).eq_iff m3 m2] at gh; exact gh

/-- Injectivity on an open set implies nonzero derivative (manifold version) -/
theorem Set.InjOn.mfderiv_ne_zero {f : S ‚Üí T} {s : Set S} (inj : InjOn f s) (so : IsOpen s) {c : S}
    (m : c ‚àà s) (fa : MAnalyticAt I I f c) : mfderiv I I f c ‚â† 0 := by
  contrapose inj; simp only [not_not, InjOn, not_forall] at inj ‚ä¢
  rcases not_local_inj_of_mfderiv_zero fa inj with ‚ü®g, ga, gc, fg‚ü©
  have gm : ‚àÄ·∂† z in ùìù c, g z ‚àà s :=
    ga.continuousAt.eventually_mem (so.mem_nhds (by simp only [gc, m]))
  replace fg := fg.and (((so.eventually_mem m).and gm).filter_mono nhdsWithin_le_nhds)
  rcases @Filter.Eventually.exists _ _ _ (AnalyticManifold.punctured_nhds_neBot I c) fg
    with ‚ü®z, ‚ü®gz, fg‚ü©, zs, gs‚ü©
  use g z, gs, z, zs, fg, gz
