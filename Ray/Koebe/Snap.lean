import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.SpecialFunctions.Complex.Arg

/-!
## Snap a complex number to `Circle`
-/

open Complex (arg I)
open Set
open scoped Real Topology ComplexConjugate
noncomputable section

variable {X : Type} [TopologicalSpace X]

/-- `z / abs z : Circle` -/
def snap (z : ‚ÑÇ) : Circle :=
  if n : z = 0 then 1 else ‚ü®z / ‚Äñz‚Äñ, by simp [n, Submonoid.unitSphere, div_self]‚ü©

lemma coe_snap {z : ‚ÑÇ} (z0 : z ‚â† 0) : (snap z).val = z / ‚Äñz‚Äñ := by
  simp only [snap, z0, ‚ÜìreduceDIte, div_eq_mul_inv]

@[simp] lemma norm_snap {z : ‚ÑÇ} : ‚Äñ(snap z).val‚Äñ = 1 := by
  simp only [snap, norm_eq_of_mem_sphere]

@[simp] lemma arg_snap {z : ‚ÑÇ} (z0 : z ‚â† 0) : arg (snap z) = arg z := by
  simp only [snap, z0, ‚ÜìreduceDIte, div_eq_mul_inv]
  have e0 : arg ((‚Äñz‚Äñ : ‚ÑÇ))‚Åª¬π = 0 := by
    simp only [‚Üê Complex.ofReal_inv]
    apply Complex.arg_ofReal_of_nonneg
    bound
  rw [Complex.arg_mul z0]
  ¬∑ simp only [add_eq_left, e0]
  ¬∑ simpa only [ne_eq, inv_eq_zero, Complex.ofReal_eq_zero, norm_eq_zero]
  ¬∑ simp only [e0, add_zero, Complex.arg_mem_Ioc]

lemma snap_eq_snap_iff {z w : ‚ÑÇ} (z0 : z ‚â† 0) (w0 : w ‚â† 0) : snap z = snap w ‚Üî arg z = arg w := by
  simp only [Circle.ext_iff, Complex.ext_norm_arg_iff, norm_snap, arg_snap z0, arg_snap w0,
    true_and]

@[simp] lemma snap_mul {z w : ‚ÑÇ} (z0 : z ‚â† 0) (w0 : w ‚â† 0) : snap (z * w) = snap z * snap w := by
  simp only [snap, mul_eq_zero, z0, w0, or_self, ‚ÜìreduceDIte, Complex.norm_mul, Complex.ofReal_mul,
    div_eq_mul_inv, mul_inv_rev, Circle.ext_iff, Circle.coe_mul]
  ring

@[simp] lemma snap_zero : snap 0 = 1 := by
  simp only [snap, ‚ÜìreduceDIte]

@[simp] lemma snap_of_pos {t : ‚Ñù} (t0 : 0 < t) : snap (t : ‚ÑÇ) = 1 := by
  simp only [snap, Complex.ofReal_eq_zero, t0.ne', ‚ÜìreduceDIte, Complex.norm_real, Real.norm_eq_abs,
    abs_of_pos t0, ne_eq, not_false_eq_true, div_self, Circle.ext_iff, OneMemClass.coe_one]

@[simp] lemma snap_mul_of_pos {t : ‚Ñù} (t0 : 0 < t) {z : ‚ÑÇ} : snap (t * z) = snap z := by
  simp only [snap, mul_eq_zero, Complex.ofReal_eq_zero, t0.ne', false_or, Complex.norm_mul,
    Complex.norm_real, Real.norm_eq_abs, abs_of_pos t0, Complex.ofReal_mul, div_mul_eq_div_div,
    ne_eq, not_false_eq_true, mul_div_cancel_left‚ÇÄ]

@[simp] lemma snap_circle (z : Circle) : snap z.val = z := by
  simp only [snap, Circle.coe_ne_zero, ‚ÜìreduceDIte, norm_eq_of_mem_sphere, Complex.ofReal_one,
    div_one, Subtype.coe_eta]

@[simp] lemma norm_mul_snap {z : ‚ÑÇ} (z0 : z ‚â† 0) : ‚Äñz‚Äñ * (snap z).val = z := by
  have n : (‚Äñz‚Äñ : ‚ÑÇ) ‚â† 0 := by simpa
  simp only [snap, z0, ‚ÜìreduceDIte, mul_div_cancel‚ÇÄ _ n]

/-- Alternative definition using `Set.codRestrict` -/
lemma snap_eq_restrict :
    snap = codRestrict (fun z : ‚ÑÇ ‚Ü¶ if z = 0 then 1 else z / ‚Äñz‚Äñ) (Submonoid.unitSphere ‚ÑÇ)
      (by intro z; simp only; split_ifs with h; all_goals simp [h]) := by
  ext z
  by_cases z0 : z = 0
  all_goals simp [z0, coe_snap]

lemma continuousAt_snap {z : ‚ÑÇ} (z0 : z ‚â† 0) : ContinuousAt snap z := by
  rw [snap_eq_restrict, continuousAt_codRestrict_iff]
  have e : ‚àÄ·∂† w : ‚ÑÇ in ùìù z, (if w = 0 then 1 else w / ‚Äñw‚Äñ) = w / ‚Äñw‚Äñ := by
    filter_upwards [eventually_ne_nhds z0]
    aesop
  refine ContinuousAt.congr_of_eventuallyEq ?_ e
  exact continuousAt_id.div (Complex.continuous_ofReal.comp continuous_norm).continuousAt
    (by simpa only [ne_eq, Complex.ofReal_eq_zero, norm_eq_zero])

@[fun_prop] lemma ContinuousAt.snap_units {f : X ‚Üí ‚ÑÇÀ£} {x : X} (fc : ContinuousAt f x) :
    ContinuousAt (fun x ‚Ü¶ snap (f x)) x := by
  refine (continuousAt_snap ?_).comp ?_
  ¬∑ apply Units.ne_zero
  ¬∑ exact Units.continuous_val.continuousAt.comp fc

@[fun_prop] lemma Continuous.snap_units {f : X ‚Üí ‚ÑÇÀ£} (fc : Continuous f) :
    Continuous (fun x ‚Ü¶ snap (f x)) := by
  rw [continuous_iff_continuousAt]
  fun_prop

lemma snap_unit (z : ‚ÑÇÀ£) : snap z = ‚ü®z / ‚Äñz.val‚Äñ, by simp [Submonoid.unitSphere]‚ü© := by
  simp only [snap, Units.ne_zero, ‚ÜìreduceDIte]

@[simp] lemma snap_exp_mul_I {t : ‚Ñù} : snap (Complex.exp (t * I)) = Circle.exp t := by
  simp [Circle.ext_iff, coe_snap]
